# QUERY 1 — All mayors with birthdates

PREFIX : <http://www.semanticweb.org/hp/ontologies/2025/10/untitled-ontology-9/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?mayor ?name ?birth WHERE {
  ?mayor a :Mayor ;
         :fullName ?name ;
         :birthDate ?birth .
}
ORDER BY ?birth


# QUERY 2 — Born after 1940 AND ordinal > 105

PREFIX : <http://www.semanticweb.org/hp/ontologies/2025/10/untitled-ontology-9/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?mayor ?name ?birth ?ordinal WHERE {
  ?mayor a :Mayor ;
         :fullName ?name ;
         :birthDate ?birth ;
         :servedTerm ?term .

  ?term :ordinal ?ordinal .

  FILTER (?birth >= "1940-01-01T00:00:00"^^xsd:dateTime)
  FILTER (?ordinal > 105)
}
ORDER BY ?birth


# QUERY 3 — All terms with optional end + vote

PREFIX : <http://www.semanticweb.org/hp/ontologies/2025/10/untitled-ontology-9/>
SELECT ?term ?ordinal ?start ?end ?vote WHERE {
  ?term a :Term ;
        :ordinal ?ordinal ;
        :termStart ?start .
        
  OPTIONAL { ?term :termEnd ?end }
  OPTIONAL { ?term :votePercentage ?vote }
}
ORDER BY ?ordinal


# QUERY 4 — Aggregation

PREFIX : <http://www.semanticweb.org/hp/ontologies/2025/10/untitled-ontology-9/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>SELECT ?mayor ?name 
       (COUNT(?term) AS ?termsServed) 
       (AVG(?vote) AS ?avgVote) 
WHERE {
  ?mayor a :Mayor ;
         :fullName ?name ;
         :servedTerm ?term .
         
  OPTIONAL { ?term :votePercentage ?vote }
}
GROUP BY ?mayor ?name
ORDER BY DESC(?termsServed)


# QUERY 5 – Succession chain

PREFIX : <http://www.semanticweb.org/hp/ontologies/2025/10/untitled-ontology-9/>
SELECT ?predecessor ?predName ?predOrd 
       ?successor ?succName ?succOrd 
WHERE {
  ?predTerm a :Term ;
            :succeededBy ?succTerm ;
            :ordinal ?predOrd .

  ?succTerm :ordinal ?succOrd .

  ?predecessor a :Mayor ;
               :fullName ?predName ;
               :servedTerm ?predTerm .

  ?successor a :Mayor ;
             :fullName ?succName ;
             :servedTerm ?succTerm .
}
ORDER BY ?predOrd

